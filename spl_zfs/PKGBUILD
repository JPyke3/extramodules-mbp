# Maintainer: Philip MÃ¼ller <philm@manjaro.org>
# Contributor: Helmut Stult <helmut[at]manjaro[dot]org>
# Contributor: satmandu

_linuxprefix=linux56-mbp
_extramodules=extramodules-5.6-MANJARO-mbp
pkgbase=$_linuxprefix-spl_zfs
pkgname=("$_linuxprefix-zfs")
pkgver=0.8.4
pkgrel=8
url="http://zfsonlinux.org/"
arch=('i686' 'x86_64')
license=("CDDL")
depends=("$_linuxprefix" "kmod")
makedepends=("$_linuxprefix-headers")
groups=("$_linuxprefix-extramodules")
install=install
source=("https://github.com/zfsonlinux/zfs/releases/download/zfs-${pkgver}/zfs-${pkgver}.tar.gz")
sha256sums=('2b988f5777976f09d08083f6bebf6e67219c4c4c183c1f33033fb7e5e5eacafb')

prepare() {
  cd "${srcdir}/zfs-${pkgver}"
  # patches here

}

build() {
  _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
  cd "${srcdir}/zfs-${pkgver}"
  ./autogen.sh
  sed -i "s|\$(uname -r)|${_kernver}|g" configure
  ./configure --prefix=/usr --sysconfdir=/etc --sbindir=/usr/bin --libdir=/usr/lib \
              --datadir=/usr/share --includedir=/usr/include --with-udevdir=/lib/udev \
              --libexecdir=/usr/lib/zfs-${pkgver} --with-config=kernel \
              --with-linux=/usr/lib/modules/${_kernver}/build \
              --with-linux-obj=/usr/lib/modules/${_kernver}/build
  make
}

package_linux56-zfs(){
  pkgdesc='Kernel modules for the Zettabyte File System.'
  provides=("zfs=$pkgver")
  depends+=("zfs-utils=${pkgver}")
  replaces=('linux54-spl<=0.7.13')

  cd "${srcdir}/zfs-${pkgver}"
  install -dm755 "$pkgdir/usr/lib/modules/$_extramodules"
  install -m644 module/*/*.ko "$pkgdir/usr/lib/modules/$_extramodules"
  find "$pkgdir" -name '*.ko' -exec gzip -9 {} +
  sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='$_extramodules'/" "$startdir/install"
}
